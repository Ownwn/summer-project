#state machine for altering records actions
@abstract
class_name RecordState extends RefCounted
var parent : Record
@abstract
func _on_area_3d_mouse_entered() -> void
@abstract
func _on_area_3d_mouse_exited() -> void
@abstract
func _on_mouse_clicked() -> void

class DisplayRecord extends RecordState:
	var record_hovering = false
	func _init(parent_record : Record):
		parent = parent_record
	
	func _on_area_3d_mouse_entered():
		record_hovering = true
		var newShader = ShaderMaterial.new()
		newShader.shader = parent.hover_shader
		parent.case.material_overlay = newShader
		parent.image_plane.material_overlay = newShader
	
	func _on_area_3d_mouse_exited() -> void:
		record_hovering = false
		parent.case.material_overlay = ShaderMaterial.new()
		parent.image_plane.material_overlay = ShaderMaterial.new()
	
	#when record is at rest, we want to view it when clicked
	func _on_mouse_clicked() -> void:
		if(Input.is_action_just_pressed("click") && record_hovering && !Global.record_in_use):
			#var target_vector = Vector3(1.087,1.069,2.226)
			var target_vector = (parent.camera.global_position*0.65)+Vector3(0.0,0.3,0.0)
			parent.global_position = target_vector
			parent.global_rotation = parent.camera.global_rotation
			parent.animator.stop()
			parent.record_state = ViewingRecord.new(parent)
			Global.record_in_use = true
			MusicManager.CURRENT_IN_USE_RECORD = parent

#functionality record inside shelf/ at rest
class StoredRecord extends RecordState:
	var record_hovering = false
	func _init(parent_record : Record) -> void:
		parent = parent_record
		
	#this functionality you will see everywhere (minus the animator),
	#it is for applying a shader to signal to the user what they are hovering over
	func _on_area_3d_mouse_entered() -> void:
		record_hovering = true
		var newShader = ShaderMaterial.new()
		newShader.shader = parent.hover_shader
		parent.case.material_overlay = newShader
		parent.image_plane.material_overlay = newShader
		if(!parent.animator.is_playing()):
			parent.animator.play("reveal_record")
			if(!parent.animator.animation_finished.is_connected(_play_backwards_anim)):
				parent.animator.animation_finished.connect(_play_backwards_anim)

	func _on_area_3d_mouse_exited() -> void:
		record_hovering = false
		parent.case.material_overlay = ShaderMaterial.new()
		parent.image_plane.material_overlay = ShaderMaterial.new()
		if(parent.animator.animation_finished.get_connections().size() > 0 && !parent.animator.is_playing()):
			_play_backwards_anim("help")

	func _play_backwards_anim(_anim_name : String) -> void:
		if(record_hovering): return
		parent.animator.play_backwards("reveal_record")
		parent.animator.animation_finished.disconnect(_play_backwards_anim)

	#when record is at rest, we want to view it when clicked
	func _on_mouse_clicked() -> void:
		if(Input.is_action_just_pressed("click") && record_hovering && !Global.record_in_use):
			#var target_vector = Vector3(1.087,1.069,2.226)
			var target_vector = (parent.camera.global_position*0.65)+Vector3(0.0,0.3,0.0)
			parent.global_position = target_vector
			parent.global_rotation = parent.camera.global_rotation
			parent.animator.stop()
			parent.record_state = ViewingRecord.new(parent)
			Global.record_in_use = true
			MusicManager.CURRENT_IN_USE_RECORD = parent

#functionality for when we want to view and use a record
class ViewingRecord extends RecordState:
	var hovering = false
	var disk_peeked = false
	func _init(parent_record : Record) -> void:
		parent = parent_record
		parent.animator.stop()
		_on_area_3d_mouse_entered()
		
	func _on_area_3d_mouse_entered() -> void:
		var newShader = ShaderMaterial.new()
		newShader.shader = parent.hover_shader
		parent.case.material_overlay = newShader
		parent.image_plane.material_overlay = newShader
		hovering = true

	func _on_area_3d_mouse_exited() -> void:
		parent.case.material_overlay = ShaderMaterial.new()
		parent.image_plane.material_overlay = ShaderMaterial.new()
		hovering = false

	#more complicated mouse action than base record class,
	#as we need to have multiple actions and options for the user to choose from
	#possible TODO create a more clear tree like structure for viewing, grabbing, or vice versa the disk
	func _on_mouse_clicked() -> void:
		if(!hovering):
			return
		if(Input.is_action_just_pressed("right_click")):
			parent.global_position = parent.base_position
			parent.global_rotation = parent.base_rotation
			parent.record_state = parent.base_state
			Global.record_in_use = false
			MusicManager.CURRENT_IN_USE_RECORD = null
		if(Input.is_action_just_pressed("click")):
			if(!parent.animator.is_playing() && !disk_peeked):
				parent.animator.play("peek_disk")
				disk_peeked = true
			if(!parent.animator.is_playing() && disk_peeked):
				parent.animator.play_backwards("peek_disk")
				disk_peeked = false
		if(Input.is_action_just_pressed("scroll_up")):
			parent.global_rotation += Vector3(0.0,1.0/6.0,0.0)
		if(Input.is_action_just_pressed("scroll_down")):
			parent.global_rotation -= Vector3(0.0,1.0/6.0,0.0)
			

#functionality for when the disk has been grabbed and the record is no longer being viewed
class EmptyRecord extends RecordState:
	var hovering = false
	
	func _init(parent_record : Record) -> void:
		parent = parent_record
		parent.physics_body.freeze = false
		parent.physics_body.apply_central_force(Vector3(190.0,0.0,-190.0)) #apply a force to send the record to the table
		parent.collisions.disabled = true
		await parent.get_tree().create_timer(0.1).timeout
		parent.collisions.disabled = false
		await parent.get_tree().create_timer(1.0).timeout
	
	func _on_area_3d_mouse_entered() -> void:
		var newShader = ShaderMaterial.new()
		newShader.shader = parent.hover_shader
		parent.case.material_overlay = newShader
		parent.image_plane.material_overlay = newShader
		hovering = true
	func _on_area_3d_mouse_exited() -> void:
		parent.case.material_overlay = ShaderMaterial.new()
		parent.image_plane.material_overlay = ShaderMaterial.new()
		hovering = false
	
	#put record back in base position
	#TODO possibly refactor node structure of record to have rigidbody3d be highest level node
	#to avoid mathematical differences between node and physics body position / rotation
	func _on_mouse_clicked() -> void:
		if(Input.is_action_just_pressed("right_click") && hovering):
			parent.physics_body.freeze = true
			var target_pos : Vector3 = parent.base_position
			parent.global_position = target_pos
			parent.physics_body.global_position = target_pos
			var target_rot : Vector3 = parent.base_rotation
			parent.global_rotation = target_rot
			parent.physics_body.global_rotation = target_rot
			parent.record_state = RecordState.DisplayRecord.new(parent)
